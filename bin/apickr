#!/usr/bin/env perl

=head1 NAME

apickr - Synchronize aperture library and flickr account.

=head1 SYNOPSIS

apickr [OPTIONS] COMMAND PATH

Synchronize aperture library given by PATH with flickr.

=head1 COMMANDS

  albums        list albums
  images        list images
  flickr        sync with flickr
  flickr-albums list flickr albums
  flickr-images list flickr images

=head1 OPTIONS

 --help|-h    brief help message
 --num|-n=NUM only process num items

=head1 AUTHOR

 Nathan Gass <gass@search.ch>

=cut

use Modern::Perl;
use autodie;

use Getopt::Long qw/:config/;
use Pod::Usage;
use YAML qw/LoadFile DumpFile Load Dump/;
use DBI;
use Flickr::API2;
use File::Spec::Functions qw/splitpath catdir/;
use Browser::Open qw/open_browser/;
use List::AllUtils qw/first max min/;
use Encode qw/encode_utf8/;
use DateTime;
use DateTime::Format::Strptime;
use Carp qw/confess/;

my @podargs = (-verbose => 99, -sections => [qw/NAME SYNOPSIS COMMANDS OPTIONS/]);
my $config = {};
eval {
	$config = LoadFile(catdir($ENV{HOME}, '.apickrrc'));
};
my $opts = {};
GetOptions(
	$opts,
	"help|h", 'num|n=i',
	) or pod2usage(-exitval => 2, @podargs);
pod2usage(@podargs) if $opts->{help};
$config = {%$config, %$opts};

my $command = shift() || "albums";
my $path = shift() || catdir($ENV{HOME}, 'Pictures', '2 Sterne.aplibrary');

given ($command) {
	when ("albums") {
		display(aperture_select(
			"RKFolder.name AS album,COUNT(DISTINCT COALESCE(RKVersion.stackUuid,RKVersion.uuid)) AS stacks,COUNT(DISTINCT RKVersion.uuid) AS images", 
			"GROUP BY RKVersion.projectUuid"
		));
	}
	when ("images") {
		display(aperture_select(
			"RKVersion.uuid,RKFolder.name AS album,RKVersion.name,imagePath,mainRating,versionNumber,RkVersion.imageDate"
		));
	}
	when ("flickr") {
	}
	when ("flickr-albums") {
		display(filter_keys(
			qr/title|photos|count_views|count_comments/,
			flickr_select('flickr.photosets.getList', {})
		));
	}
	when ("flickr-images") {
		display(filter_keys(
			qr/title|datetaken\b|tags|views/,
			flickr_select(
				'flickr.people.getPhotos', 
				{user_id => 'me', sort => 'date-taken-asc', extras => 'date_taken,date_upload,tags,views'},
			)
		));
	}
	default {die "Unknown command $_"}
};

exit;

sub flickr_auth {
	my $api = shift;
	my $auth = {};
	my $authpath = catdir($ENV{HOME}, '.apickr.auth');
	if (-e $authpath) {
		$auth = LoadFile($authpath);
	}
	if (!$auth->{token}) {
		my $frob = flickr_api('flickr.auth.getFrob', api_key => $api->raw->{api_key});
		my $url = $api->raw->request_auth_url('write', $frob);
		open_browser($url);
		say "Please authorize apickr to read and write to your flickr account.";
		say "Press any key to continue.";
		local $| = 0;
		getc;
		$auth = flickr_api('flickr.auth.getToken', frob => $frob);
		DumpFile($authpath, $auth);
	}
	return $auth;
}

sub flickr_api {
	state $api = Flickr::API2->new({key => '04c25240b84f6f5d32e8e43fd4deb249', secret => 'abef06bf4946a4a2'});
	state $auth = flickr_auth($api);
	my $method = shift;
	my $params = {@_};
	$params->{auth_token} = $auth->{token} 
		if $auth;
	my $req = $api->execute_method($method, $params);
	if ($req->{stat} eq 'ok') {
		delete $req->{stat};
		my ($first) = keys %$req;
		return simplify_flickr_response($req->{$first});
	}
}

sub simplify_flickr_response {
	my $r = shift;
	given (ref $r) {
		when ('HASH') {
			if (exists $r->{_content}) {
				return $r->{_content};
			} else {
				return { map {$_ => simplify_flickr_response($r->{$_})} keys %$r };
			}
		}
		when ('ARRAY') {
			return [map {simplify_flickr_response($_)} @$r];
		}
		default {
			return $r;
		}
	} 
}

sub flickr_select {
	my ($method, $args, $select) = @_;
	my ($resp, $page, $key, $i);
	$args->{per_page} = $opts->{num} ? min(500, $opts->{num} * 10) : 500;
	return sub {
		if (!$i || $i == @{$resp->{$key}}) {
			return undef if $key && @{$resp->{$key}} < 500;
			$args->{page} = ++$page;
			$resp = flickr_api($method, %$args);
			$key = first {ref($resp->{$_}) eq 'ARRAY'} keys %$resp;
			$i = 0;
		}
		my $res = $resp->{$key}[$i++];
		foreach my $k (keys %$res) {
			$res->{$k} = datetime($res->{$k}) if $k =~ /date/;
		}
		return $res;
	}
}

sub aperture_select {
	my ($select, $add) = @_;
	$add = "" unless $add;
	my $dbpath = catdir($path, 'Database', 'Library.apdb');
	my $dbh = DBI->connect("dbi:SQLite:dbname=$dbpath","", "", { RaiseError => 1, AutoCommit => 0, ReadOnly => 1});
	my $sth = $dbh->prepare(
		"Select $select FROM RKVersion"
		. " JOIN RKFolder ON RKFolder.uuid = RKVersion.projectUuid"
		. " JOIN RKMaster ON RKVersion.masterUuid = RKMaster.uuid"
		. " WHERE RKVersion.isInTrash = 0 AND RKVersion.versionNumber > 0 AND RKFolder.folderType = 2 $add"
		);
	$sth->execute();
	return sub {
		my $row = $sth->fetchrow_hashref;
		if ($row && $row->{imagePath}) {
			my ($d,$dir,$name) = splitpath($row->{imagePath});
			my $prev = catdir($path, 'Previews', $dir, $row->{uuid}, $row->{name} . ".jpg");
			$row->{path} = -e $prev ? $prev : catdir($path, 'Masters', $row->{imagePath});
		}
		if ($row && $row->{imageDate}) {
			$row->{imageDate} = datetime($row->{imageDate});
		}
		$dbh->disconnect unless $row;
		return $row;
	}
}

sub datetime {
	state $formatter = DateTime::Format::Strptime->new(pattern => '%Y-%m-%d %H:%M:%S');
	my ($val, $res) = @_;
	if ($val =~ /^\d+$/) { # aperture datetime
		$res = DateTime->from_epoch(epoch => $val, formatter => $formatter);
		$res->set(year => $res->year + 31);
	} else {               # flickr datetime
		$res = $formatter->parse_datetime($val);
		$res->set_formatter($formatter);
	}
	return $res;
}

sub filter_keys {
	my ($select, $gen) = @_;
	return sub {
		my $row = $gen->();
		return $row ? {map {$_ ~~ $select ? ($_ => $row->{$_}) : ()} keys %$row} : undef;
	}
}

sub display {
	state $num = 0;
	my ($gen) = @_;
	my ($row, $n);
	say d($row) while ($row = $gen->()) && (!$opts->{num} || $n++ < $opts->{num});
}

sub d {
	my ($row) = @_;
	my $res = {};
	confess "Can only handles hashes" unless ref($row) eq 'HASH';
	$res->{$_} = $row->{$_} ? encode_utf8($row->{$_}) : $row->{$_} foreach keys %$row;
	return Dump $res;
}

